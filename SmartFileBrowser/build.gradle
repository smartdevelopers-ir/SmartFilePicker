plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdkVersion 36

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 36

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'ir.smartdevelopers.smartfilebrowser'
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}
group = 'ir.smartdevelopers'
version = '2.0.2'

dependencies {

    implementation libs.appcompat
    implementation libs.androidxcore
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout

    implementation libs.glide

    annotationProcessor libs.compiler
    implementation libs.ahbottomnavigation

    implementation libs.photoeditor
//    implementation libs.wix.playground.ahbottomnavigation
}
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                from components.release

                groupId = 'ir.smartdevelopers'
                artifactId = 'smart-file-picker'

                pom {
                    name = 'smart-file-picker'
                    description = 'pick file easilly'
                    url = 'https://github.com/smartdevelopers-ir/SmartFilePicker'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'smartdevelopers'
                            name = 'mostafa babaei'
                            email = 'info@smartdevelopers.ir'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/smartdevelopers-ir/SmartFilePicker.git'
                        developerConnection = 'scm:git:ssh://github.com/smartdevelopers-ir/SmartFilePicker.git'
                        url = 'http://github.com/smartdevelopers-ir/SmartFilePicker'
                    }

                }
            }
        }
        repositories {
            maven {
                // change URLs to point to your repos, e.g. http://my.org/repo
                def releasesRepoUrl = "$buildDir/repos/releases"
                def snapshotsRepoUrl = "$buildDir/repos/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

signing {
    sign configurations.archives
}
